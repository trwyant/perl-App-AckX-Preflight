#!/usr/bin/env perl

use 5.010001;

use strict;
use warnings;

use Getopt::Long 2.33 qw{ :config auto_version };
use App::Ack::File;
use App::AckX::Preflight::Syntax ();	# DO NOT run import()
use App::AckX::Preflight::Util qw{ :json };

our $VERSION = '0.000_044';

my %opt;

GetOptions( \%opt,
    App::AckX::Preflight::Syntax->__main_parser_options(),
    help => sub {
	require Pod::Usage;
	Pod::Usage::pod2usage( { -verbose => 2 } );
    },
) or do {
    require Pod::Usage;
    Pod::Usage::pod2usage( { -verbose => 0 } );
};

keys %opt
    or %opt = ( syntax => [ 'code' ] );

App::AckX::Preflight::Syntax->__normalize_options( \%opt );

my $path = @ARGV ? $ARGV[0] : 't/data/perl_file.PL';

# We do NOT want to run import() yet.
require App::AckX::Preflight::FileMonkey;

my $arg = __json_encode( [
	[ 'App::AckX::Preflight::Syntax' => \%opt ],
    ],
);

App::AckX::Preflight::FileMonkey->import( $arg );

my $file = App::Ack::File->new( $path );

my $fh = $file->open();

while ( <$fh> ) {
    print;
}

__END__

=head1 TITLE

runtime - Exercise the ack end of the App::AckX::Preflight syntax functionality, only at run time, not compile time.

=head1 SYNOPSIS

 runtime
 runtime --syntax=code t/data/cc_file.c
 runtime --help
 runtime --version

=head1 OPTIONS

All the L<App::AckX::Preflight::Syntax|App::AckX::Preflight::Syntax>
options are accepted. If none are specified, the default is
C<--syntax=code>.

In addition, the following options are provided:

=head2 --help

This option displays the documentation for this script. The script then
exits.

=head2 --version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script executes the F<ack> end of the
L<App::AckX::Preflight::Syntax|App::AckX::Preflight::Syntax>
functonality, only at run time rather than compile time. Because it is
invoked directly it is more accessable for debugging.

The argument is the path to the file to be processed. If no argument is
provided, F<t/data/perl_file.PL> is processed.

B<Only> L<App::AckX::Preflight::Syntax|App::AckX::Preflight::Syntax>
functionality is provided. That is, syntax filtering will be performed,
but not searching.

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2022-2023 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
