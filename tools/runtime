#!/usr/bin/env perl

use 5.010001;

use strict;
use warnings;

use Getopt::Long 2.33 qw{ :config auto_version pass_through };
use App::AckX::Preflight::Syntax ();	# DO NOT run import()
use App::AckX::Preflight::Util qw{
    :json
    :syntax
    __interpret_plugins
    __load_module
    ACK_FILE_CLASS
};

our $VERSION = '0.000_047';

use constant SYNTAX_PLUGIN	=> 'App::AckX::Preflight::Plugin::Syntax';

__load_module( ACK_FILE_CLASS );
__load_module( SYNTAX_PLUGIN );

my %opt;

GetOptions( \%opt,
    help => sub {
	require Pod::Usage;
	Pod::Usage::pod2usage( { -verbose => 2 } );
    },
) or do {
    require Pod::Usage;
    Pod::Usage::pod2usage( { -verbose => 0 } );
};

my ( $plugin_info ) = __interpret_plugins( SYNTAX_PLUGIN );
my $config = $plugin_info ? $plugin_info->{opt} : {};

keys %{ $config }
    or $config->{syntax} = [ SYNTAX_CODE ];

my $path = @ARGV ? $ARGV[0] : 't/data/perl_file.PL';

# We do NOT want to run import() yet.
require App::AckX::Preflight::FileMonkey;

my $arg = __json_encode( [
	[ 'App::AckX::Preflight::Syntax' => $config ],
    ],
);

App::AckX::Preflight::FileMonkey->import( $arg );

my $file = ACK_FILE_CLASS->new( $path );

my $fh = $file->open();

while ( <$fh> ) {
    print;
}

$file->close();

__END__

=head1 TITLE

runtime - Exercise the ack end of the App::AckX::Preflight syntax functionality, only at run time, not compile time.

=head1 SYNOPSIS

 runtime
 runtime --syntax=code t/data/cc_file.c
 runtime --help
 runtime --version

=head1 OPTIONS

All the L<App::AckX::Preflight::Syntax|App::AckX::Preflight::Syntax>
options are accepted. If none are specified, the default is
C<--syntax=code>.

In addition, the following options are provided:

=head2 --help

This option displays the documentation for this script. The script then
exits.

=head2 --version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script executes the F<ack> end of the
L<App::AckX::Preflight::Syntax|App::AckX::Preflight::Syntax>
functonality, only at run time rather than compile time. Because it is
invoked directly it is more accessable for debugging.

The argument is the path to the file to be processed. If no argument is
provided, F<t/data/perl_file.PL> is processed.

B<Only> L<App::AckX::Preflight::Syntax|App::AckX::Preflight::Syntax>
functionality is provided. That is, syntax filtering will be performed,
but not searching.

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2022-2023 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
