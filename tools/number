#!/usr/bin/env perl

use 5.010001;

use strict;
use warnings;

use App::AckX::Preflight::Plugin::Syntax;
use App::AckX::Preflight::Syntax ();
use App::AckX::Preflight::Util qw{
    ACK_FILE_CLASS
    __interpret_plugins
    __load_module
};
use Getopt::Long 2.33 qw{ :config auto_version pass_through };
use Pod::Usage;
use Text::Abbrev;

use lib qw{ inc };
use My::Module::TestSyntax;

our $VERSION = '0.000_046';

use constant SYNTAX_CLASS	=> 'App::AckX::Preflight::Syntax';
use constant SYNTAX_PLUGIN	=> 'App::AckX::Preflight::Plugin::Syntax';

my %opt;

GetOptions( \%opt,
    qw{ tell! },
    help => sub { pod2usage( { -verbose => 2 } ) },
) and @ARGV >= 1 or pod2usage( { -verbose => 0 } );

my ( $plugin_info ) = __interpret_plugins( SYNTAX_PLUGIN );
my $config = $plugin_info ? $plugin_info->{opt} : {};

my $path = pop @ARGV;

__load_module( ACK_FILE_CLASS )
    or die "Failed to load @{[ ACK_FILE_CLASS ]}: $@";

-e $path
    or die "File $path not found\n";

my %slurp_opt;
exists $opt{tell}
    and $slurp_opt{tell} = delete $opt{tell};

my $file = ACK_FILE_CLASS->new( $path );

sub SYNTAX_FILTER {
    return SYNTAX_CLASS->__get_syntax_filter( $file );
}

setup_syntax( %{ $config } );

print slurp( $file, \%slurp_opt );

__END__

=head1 TITLE

number - Number the lines of files consistently with t/syntax_*.t

=head1 SYNOPSIS

 number <foo
 number foo
 number --help
 number --version

=head1 OPTIONS

All options supported by
L<App::AckX::Preflight::Plugin::Syntax|App::AckX::Preflight::Plugin::Syntax>
are accepted, though not all actually do something. The following
options specific to this script are also supported:

=head2 --help

This option displays the documentation for this script. The script then
exits.

=head2 --tell

If this Boolean option is asserted, than the position in the file
B<after> the line is read is added to the output.

The default is C<--no-tell>.

=head2 --version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script prints a file to standard out, with lines numbered
consistently with the usage in F<t/syntax_*.t>. The file name is given
on the command line. If there is none, C<STDIN> is read.

This script must be run from the home directory of this package, because
it makes use of C<My::Module::TestSyntax>, which is in the F<inc/>
directory of the package.

This Perl script is completely unsupported, and may be changed or
removed without notice.

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2018-2023 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
